apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '23'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 143
        versionName '5.0-BETA'
    }
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        debug.initWith(buildTypes.debug)
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            manifestPlaceholders = [bugsnagApiKey: "", ormLog: false]
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release

            def Properties bugsnagProps = new Properties()
            bugsnagProps.load(new FileInputStream(file('../bugsnag.properties')))
            manifestPlaceholders = [bugsnagApiKey: bugsnagProps["api-key"], ormLog: false]
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions{
        abortOnError false
    }

    dexOptions{
        preDexLibraries = !'true'.equals(System.getenv('CI'))
    }
}

dependencies {
    compile "com.android.support:support-v4:$rootProject.ext.supportVersion"
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:support-annotations:$rootProject.ext.supportVersion"
    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.loopeer.android.thirdparty:swipe-back-layout:1.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile "com.jakewharton.rxbinding:rxbinding:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rootProject.ext.rxBindingVersion"
    compile "com.jakewharton.rxbinding:rxbinding-design:$rootProject.ext.rxBindingVersion"
    compile "com.trello:rxlifecycle:$rootProject.ext.rxLifecycleVersion"
    compile "com.trello:rxlifecycle-components:$rootProject.ext.rxLifecycleVersion"
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    compile('com.mikepenz:materialdrawer:3.0.6@aar') {
        transitive = true
    }
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile "com.github.johnkil.android-robototextview:robototextview:$rootProject.ext.robotoTextViewVersion"
    compile "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    compile "com.facebook.stetho:stetho-okhttp:$rootProject.ext.stethoVersion"
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.github.satyan:sugar:1.3.1'
    compile 'com.github.Groxx:AndroidSwipeLayout:c8a42d8'
    compile "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    apt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    apt "com.bluelinelabs:logansquare-compiler:$rootProject.ext.loganSquareVersion"
    compile "com.bluelinelabs:logansquare:$rootProject.ext.loganSquareVersion"
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.bugsnag:bugsnag-android:3.2.6'
}


android.applicationVariants.all { variant ->
    variant.mergeAssets.doFirst {
        File fonts = file("${buildDir}/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/${rootProject.ext.robotoTextViewVersion}/assets/fonts")
        if (fonts.exists()) {
            for (File file : fonts.listFiles()) {
                if (file.getName().contains("RobotoSlab")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
                if (file.getName().contains("Roboto-Black")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
                if (file.getName().contains("RobotoCondensed")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
                if (file.getName().contains("Roboto-BlackItalic")) {
                    println("delete " + file.getName() + " font")
                    file.delete()
                };
            }
        }
    }
}
